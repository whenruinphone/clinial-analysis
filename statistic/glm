**a.采用广义线性模型（GLM，模型中包括组别和分层因素）估计两组ORR及其95%置信区间，两组ORR比值及90%置信区间（基于log link function），
两组ORR差值及90%置信区间（基于identity link function）。分层因素为性别、EGFR基因状态（野生型vs.突变型），和有无脑转移。 
b.广义线性模型中只包括组别。;

ods graphics off;
**ods trace on;

**含分层因素*;
**差值*;
proc glimmix data=adrs;
	class trt01pn (ref=last) rmbfn sexn egfrn;
	model orr=trt01pn rmbfn sexn egfrn/dist=binary link=identity;
    lsmeans trt01pn/cl ilink alpha=0.05;
	estimate 'RD for trt 90' trt01pn 1-1/alpha=0.1;
	ods output  LSmeans=stat_est1
                Estimates=stat_diff1(where=(label='RD for trt 90'));
run;

**比值*;
proc glimmix data=adrs;
	class trt01pn(ref=last) rmbfn sexn egfrn;
	model orr=trt01pn rmbfn sexn egfrn/dist=binary link=log;
	estimate 'RR for trt 90' trt01pn 1-1/exp alpha=0.1;
	nloptions MAXITER= 700;
	**estimate 'RR for trt 95' trt01pn 1-1/exp alpha=0.05;
	ods output Estimates=stat_rate1(where=(label='RR for trt 90')) ;
run;


**不含分层因素*;
**差值*;
proc glimmix data=adrs;
	class trt01pn (ref=last)  ;
	model orr=trt01pn/dist=binary link=identity;
    lsmeans trt01pn/cl ilink alpha=0.05;
	estimate 'RD for trt 90' trt01pn 1-1/alpha=0.1;
	ods output  LSmeans=stat_est2
                Estimates=stat_diff2(where=(label='RD for trt 90'));
run;

**比值*;
proc glimmix data=adrs;
	class trt01pn(ref=last) ;
	model orr=trt01pn /dist=binary link=log;
	estimate 'RR for trt 90' trt01pn 1-1/exp alpha=0.1;
	nloptions MAXITER= 700;
	**estimate 'RR for trt 95' trt01pn 1-1/exp alpha=0.05;
	ods output Estimates=stat_rate2(where=(label='RR for trt 90')) ;
run;

ods graphics on;
